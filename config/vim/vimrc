set nocompatible
let s:capable_t=&t_Co > 16 || has("gui_running") 			" Checking against TTYs
let s:path=fnamemodify(resolve(expand('<sfile>:p')), ':h')	" https://stackoverflow.com/questions/4976776/how-to-get-path-to-the-current-vimscript-being-executed

" Directories {{{
" Vim follows the XDG Base Directory specification, except for the 'viminfofile'
let &viminfofile =	$XDG_STATE_HOME .. '/vim/viminfo'

let &runtimepath =	$XDG_DATA_HOME	.. '/vim'	..	',' ..	&runtimepath
let &runtimepath =	s:path			..	','		..			&runtimepath " This path will be the first in the list
let &runtimepath .=	','				.. s:path	..			'/after'
" }}}
" Options {{{
set mouse=a
set belloff=all


set modeline modelines=1
set nomodelineexpr

set encoding=utf-8 fileencoding=utf-8

set updatetime=2000
" timeoutlen: mappings ; ttimeoutlen: key codes
set ttimeoutlen=50

syntax enable
filetype plugin indent on

set foldenable foldmethod=marker
set smartcase hlsearch incsearch

set noexpandtab shiftwidth=0 softtabstop=0
set autoindent smartindent tabstop=4
set textwidth=120 nowrap

set splitright
set scrolloff=6

set hidden

set path=$PWD/**
set wildmenu
set wildchar=<tab>
set wildcharm=<a-tab>
" 'suffixes' can also be used to de-prioritise files in the wildmenu
set wildignore=.git/,.svn/,.hg/,*.lock
set wildignore+=tags,build/,target/

set completeopt=menuone,popup
set completepopup=height:60,width:80,border:on,align:item,highlight:InfoPopup

set grepprg=rg\ --vimgrep
set grepformat=%f:%l:%c:%m
" }}}
" Look {{{
set number relativenumber numberwidth=6
set cursorline cursorcolumn
"set colorcolumn=100

set nolist
" For reference:
" [▼] U+25BC: Black Down-Pointing Triangle
" [▸] U+25B8: Black Right-Pointing Triangle
" [␣] U+2423: Open Box
" [•] U+2022: Bullet
if s:capable_t
	set termguicolors
	set listchars=tab:▸\ ,nbsp:▼,trail:•,eol:$
else
	set listchars=tab:»\ ,nbsp:▼,trail:•,eol:$
endif
" }}}

" Mappings {{{
"let mapleader=' '

noremap <left>	<nop>
noremap <down>	<nop>
noremap <up>	<nop>
noremap <right>	<nop>

nnoremap <leader>s :%s/\<<c-r><c-w>\>/
" }}}
" Abbreviations {{{
abbreviate teh the
" }}}
" Commands {{{
" Wayland clipboard {{{
if $XDG_SESSION_TYPE == "wayland"
	function! s:WlCopy(...)
		let l:register='0'
		if a:0 > 1
			echoerr 'WlCopy may take 1 argument naming a Vim register to read from, defaulting to \"0.'
		elseif a:0 == 1
			let l:register=a:1
		endif

		silent call system('wl-copy '..shellescape(getreg(l:register)))
	endfunction

	command! -nargs=* WlCopy call s:WlCopy(<f-args>)
	command! WlPaste silent r ! 'wl-paste'
endif
" }}}
" Execute the current file. Mnemonic: (Single|Script|Shebang)run
command! Srun :execute 'vertical terminal '..fnameescape(expand('%:p'))
" }}}
" Autocommands {{{
" Automatically read in the contents from template files when appropriate
augroup onnew_templates
	autocmd!
	autocmd BufNewFile * silent!
				\	execute '0read ' .. s:path .. '/files/templates/'..expand('<afile>:e')
				\ |	execute '$d_'
				\ |	execute '0'
augroup END

augroup onwrite_format
	autocmd!
	autocmd BufWrite *.{asm,c,h,cpp,hpp,rs,sh,zsh,py,nix} :Autoformat
augroup END
" }}}

" Additional configuration files {{{
" Kitty (the terminal) has some recommended settings regarding rendering 
if $TERM =~ "kitty"
	execute 'source ' .. s:path .. '/kitty.vim'
endif
" Only enable plugins if the plugin manager is installed, and the terminal isn't a TTY
if s:capable_t && ! empty(globpath(&rtp, 'autoload/plug.vim'))
	execute 'source ' .. s:path .. '/plugins.vim'
endif
" }}}
